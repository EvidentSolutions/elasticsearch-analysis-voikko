buildscript {
    repositories {
        mavenCentral()
        maven {
            name 'sonatype-snapshots'
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        jcenter()
    }
    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:7.7.0"
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'
apply plugin: 'nebula.maven-publish' // esplugin needs nebula
apply plugin: 'maven-publish'

group = 'fi.evident.elasticsearch'
version = '0.6.0'

javadoc {
    options.encoding = 'UTF-8'
}

dependencies {
    compile 'org.puimula.voikko:libvoikko:4.1.1'
}

esplugin {
    description 'Voikko Analysis for Elasticsearch'
    classname 'fi.evident.elasticsearch.voikko.plugin.AnalysisVoikkoPlugin'
    name 'elasticsearch-analysis-voikko'
    licenseFile rootProject.file('LICENCE')
    noticeFile rootProject.file('NOTICE')
}

test {
    if (System.getProperty('voikko.path') != null)
        systemProperty 'voikko.path', System.getProperty('voikko.path')

    if (System.getProperty('voikko.dict.path') != null)
        systemProperty 'voikko.dict.path', System.getProperty('voikko.dict.path')
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact javadocJar
            artifact sourcesJar
            artifact bundlePlugin

            pom {
                name = 'Voikko Analysis for Elasticsearch'
                description = 'The Voikko Analysis plugin provides Finnish language analysis using Voikko.'
                url = 'https://github.com/EvidentSolutions/elasticsearch-analysis-voikko'

                scm {
                    connection = 'scm:git:git://github.com/EvidentSolutions/elasticsearch-analysis-voikko.git'
                    developerConnection = 'scm:git:ssh://github.com/EvidentSolutions/elasticsearch-analysis-voikko.git'
                    url = 'https://github.com/EvidentSolutions/elasticsearch-analysis-voikko'
                }
            }
        }

        // add license information to generated poms
        all {
            pom.withXml { XmlProvider xml ->
                Node node = xml.asNode()

                Node license = node.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')

                def developers = node.appendNode('developers')

                Node dev1 = developers.appendNode('developer')
                dev1.appendNode('name', 'Juha Komulainen')
                dev1.appendNode('url', 'http://www.evident.fi')

                Node dev2 = developers.appendNode('developer')
                dev2.appendNode('name', 'Martti Soininen')
                dev2.appendNode('url', 'http://www.evident.fi')
            }
        }
    }
}

// TODO: temporary workaround until it's fixed in elasticsearch
thirdPartyAudit.enabled = false
testingConventions.enabled = false
