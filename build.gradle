// build with ex: ./gradlew clean build -Dvoikko.path=/usr/lib64 -Dvoikko.dict.path=/usr/lib/voikko/

group = 'fi.evident.elasticsearch'
version = '7.10.0'

buildscript {
    repositories {
        mavenCentral()
        maven {
            name 'sonatype-snapshots'
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        jcenter()
    }
    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:7.10.0"
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'

javadoc {
    options.encoding = 'UTF-8'
}

dependencies {
    compile ('org.puimula.voikko:libvoikko:4.1.1') {
        exclude group: 'net.java.dev.jna', module: 'jna'
    }
    testImplementation 'org.elasticsearch:jna:5.5.0'
}

esplugin {
    description 'Voikko Analysis for Elasticsearch'
    classname 'fi.evident.elasticsearch.voikko.plugin.AnalysisVoikkoPlugin'
    name project.name
    licenseFile rootProject.file('LICENCE')
    noticeFile rootProject.file('NOTICE')
}

forbiddenApisTest.enabled = false

test {
    if (System.getProperty('voikko.path') != null)
        systemProperty 'voikko.path', System.getProperty('voikko.path')

    if (System.getProperty('voikko.dict.path') != null)
        systemProperty 'voikko.dict.path', System.getProperty('voikko.dict.path')
}

// TODO: temporary workaround until it's fixed in elasticsearch
thirdPartyAudit.enabled = false
testingConventions.enabled = false
validateNebulaPom.enabled = false
loggerUsageCheck.enabled = false
licenseHeaders.enabled = false